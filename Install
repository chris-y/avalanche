(procedure p_setdeftool #type
	(set #def (cat "def_" #type))
	(tooltype
		(prompt "Setting default tool for " #type)
		(help @tooltype-help)
		(dest (tackon "ENVARC:Sys/" #def))
		(setdefaulttool (tackon @default-dest #Avalanche))
	)
)

; START
(set #os-vernum (getversion "libs:version.library"))
(set #os-ver (/ #os-vernum 65536))

(transcript "Running on OS v" #os-ver)

(if (< #os-ver 50)
	(
		(set #tools-dir "SYS:Tools")
		(set #avalanche "Avalanche")

		(set #arc-7zip  "")
		(set #arc-arj   "")
		(set #arc-bz2   "")
		(set #arc-gzip  "")
		(set #arc-lha   "")
		(set #arc-lharc "")
		(set #arc-lzx   "")
		(set #arc-pkzip "")
		(set #arc-rar   "")
		(set #arc-tar   "")
		(set #arc-zip   "")
		(set #arc-zoo   "")

		(set #xvs-ver 0)
	)
	; else
	(
		; OS4 doesn't have Tools.

		(set #tools-dir (getenv "AppDir/AvalanchePPC"))

		(if (= #tools-dir "") (set #tools-dir "SYS:Utilities"))
		(set #avalanche "AvalanchePPC")

		(set #arc-7zip  "7zip")
		(set #arc-arj   "arj")
		(set #arc-bz2   "bz2")
		(set #arc-gzip  "gzip")
		(set #arc-lha   "lha")
		(set #arc-lharc "lharc")
		(set #arc-lzx   "lzx")
		(set #arc-pkzip "pkzip")
		(set #arc-rar   "rar")
		(set #arc-tar   "tar")
		(set #arc-zip   "zip")
		(set #arc-zoo   "zoo")

		(set #xvs-vernum (getversion "libs:xvs.library"))
		(set #xvs-ver (/ #xvs-vernum 65536))
		(transcript "xvs.library v" #xvs-ver)
	)
)

(set #xad-ver (getversion "libs:xadmaster.library"))

(if (= #xad-ver 0)
	(
		(message "Please note that this program requires xadmaster.library!\n\n"
			"You will need to install it before running Avalanche. "
			"It can be downloaded from Aminet util/arc" (all))
	)
)

(if (= #xad-ver 12)
	(
		(message "Although Avalanche works with xadmaster.library v12, "
			"usage of v13 is recommended due to inconsistencies in the previous version.\n\n"
			"There is a beta build for OS3 at https://github.com/Hagbard-Celin/xadmaster_Amiga_68k "
			"- please install this if you are comfortable to do so" (all))
	)
)

(welcome)

(set @default-dest
	(askdir
		(prompt "Please select where you would like Avalanche to be installed"
				" (a drawer will NOT be created)")
		(help @askdir-help)
		(default #tools-dir)
	)
)

(set #doc-dest
	(askdir
		(prompt "Please select where you would like the Avalanche "
				"documentation to be installed (a drawer will NOT be created)")
		(help @askdir-help)
		(default "LOCALE:Help")
	)
)

(set #full-path (tackon @default-dest #Avalanche))
(set #icon-exists (exists (cat #full-path ".info")))

(copyfiles
	(prompt "Copying Avalanche")
	(help @copyfiles-help)
	(source #Avalanche)
	(dest @default-dest)
)

; copy xvs.l.main on os4
(if (>= #xvs-ver 33)
	(
		(copyfiles
			(prompt "Copying xvs.l.main")
			(help @copyfiles-help)
			(source "libs/xvs.l.main")
			(dest "libs:")
		)
	)
)

; Don't overwrite existing icon
(if (= #icon-exists 0)
	(
		(copyfiles
			(prompt "Copying Avalanche icon")
			(help @copyfiles-help)
			(source (cat #Avalanche ".info"))
			(dest @default-dest)
		)
	)
)

(copyfiles
	(prompt "Copying Avalanche Guide")
	(help @copyfiles-help)
	(source "Avalanche.guide")
	(dest #doc-dest)
)

(set #def-icons
	(askoptions
		(prompt "Set default tool for the following filetypes:")
		(help "AmigaOS 3.2 by default has one default icon for all archives. "
				"Selecting \"archive\" here will set Avalanche as the default tool "
				"for all archives.")
		(choices 	"archive"
					#arc-7zip
					#arc-arj
					#arc-bz2
					#arc-gzip
					#arc-lha
					#arc-lharc
					#arc-lzx
					#arc-pkzip
					#arc-rar
					#arc-tar
					#arc-zip
					#arc-zoo)
	)
)

(if (BITAND #def-icons 1)      (p_setdeftool "archive"))
(if (BITAND #def-icons 2)      (p_setdeftool #arc-7zip))
(if (BITAND #def-icons 4)      (p_setdeftool #arc-arj))
(if (BITAND #def-icons 8)      (p_setdeftool #arc-bz2))
(if (BITAND #def-icons 16)     (p_setdeftool #arc-gzip))
(if (BITAND #def-icons 32)     (p_setdeftool #arc-lha))
(if (BITAND #def-icons 64)     (p_setdeftool #arc-lharc))
(if (BITAND #def-icons 128)    (p_setdeftool #arc-lzx))
(if (BITAND #def-icons 256)    (p_setdeftool #arc-pkzip))
(if (BITAND #def-icons 512)    (p_setdeftool #arc-rar))
(if (BITAND #def-icons 1024)   (p_setdeftool #arc-tar))
(if (BITAND #def-icons 2048)   (p_setdeftool #arc-zip))
(if (BITAND #def-icons 4096)   (p_setdeftool #arc-zoo))
